plugins {
    id 'org.openapi.generator' version '6.0.1'
}

group 'org.skillhub'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

ext {
    lombokVersion = '1.18.24'
}

dependencies {
    annotationProcessor "org.projectlombok:lombok:${lombokVersion}"
    compileOnly "org.projectlombok:lombok:${lombokVersion}"
    implementation(
            'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.0.2',
            'org.springdoc:springdoc-openapi-security:1.6.14',
            'io.swagger.core.v3:swagger-annotations:2.2.2',
            'org.openapitools:jackson-databind-nullable:0.2.3',
            'javax.validation:validation-api:2.0.1.Final',
            'com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:2.13.3',
            'javax.annotation:javax.annotation-api:1.3.2',
            'org.springframework.boot:spring-boot-starter-web:3.0.2',
            'javax.servlet:javax.servlet-api:3.0.1'
    )
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.1'
}

openApiGenerate {
    generatorName = 'spring'
    apiPackage = "${group}.api".toString()
    modelPackage = "${group}.model".toString()
    inputSpec = "${projectDir}/src/main/resources/openapi.yaml".toString()
    outputDir = "${buildDir}/generated".toString()
    groupId = "$group".toString()
    id = "$description".toString()
    version = "$version".toString()
    configOptions = [
            interfaceOnly                 : "true",
            openApiNullable               : "false",
            additionalModelTypeAnnotations: "@lombok.Builder(toBuilder=true);" +
                    "@lombok.NoArgsConstructor;" +
                    "@lombok.AllArgsConstructor;" +
                    "@com.fasterxml.jackson.annotation.JsonIgnoreProperties(ignoreUnknown = true)"
    ]

    typeMappings = [
            "OffsetDateTime": "ZonedDateTime",
            "MyPrincipal": "java.security.Principal"
    ]
    importMappings = [
            "java.time.OffsetDateTime": "java.time.ZonedDateTime",
            "MyPrincipal": "java.security.Principal"
    ]
    schemaMappings = [
            "MyPrincipal": "java.security.Principal"
    ]
}

test {
    useJUnitPlatform()
}
sourceSets {
    main {
        java {
            srcDir(files("${openApiGenerate.outputDir.get()}/src/main"))
        }
    }
}
compileJava.dependsOn tasks.openApiGenerate
